{***********************************************
Envelope Filter Delay Lofi Shell with Wave Display and XY
Author: Native Instruments
Written by: Yaron Eshkar
Modified: July 27,  2021
*************************************************}

{
In setting up the NKI, make sure the groups have an envelope modulator for the volume parameter (like the default Kontakt group) at the first modulator slot (index 0).

The script scales to whatever number of groups are present in the NKI. 
After adding or removing groups the script needs to be re-applyed and then the source parameter will be updated with the new number of groups.

The graphical assets for the instrument can be easily replaced and will still perfectly fit the layout if the same sizes are used for the assets.
The file for the XY cursor is "cursor.png", the size is 21x20 pixels.
The file for the knobs is  "knob.png", the animation  is 31 vertical frames, each frame is 64*64 pixels for a total of 64*1984 pixels.
The file for the instrument icon is "icon.png", the size is 33x34 pixels.
}

on init
    message("") { Reset the messages }

    declare @instrumentName { Declare the instrument name variable }
    @instrumentName := "O30M Synth" { Set the instrument name string }

    declare ui_label $labelInstrumentName (8, 1) { Label for the full instrument name }
    set_script_title(@instrumentName) { Set the script title }
    set_snapshot_type(1) { Don't init on snapshots }

    if ($NI_KONTAKT_IS_HEADLESS = 0) { Check if running headless, only do GUI stuff if on desktop }
        make_perfview { Make the performance view }
        set_ui_height(8) { Set the height of the UI }
        set_ui_color(000000h) { Background to black }
        set_control_par_str($INST_ICON_ID, $CONTROL_PAR_PICTURE, "icon") { instrument icon }
        set_text($labelInstrumentName,  @instrumentName) { Set the instrument name label }
        set_control_par(get_ui_id($labelInstrumentName), $CONTROL_PAR_TEXT_ALIGNMENT, 1) { Center the text }
        set_control_par(get_ui_id($labelInstrumentName), $CONTROL_PAR_WIDTH, 300) { Make the label wide enough }
        set_control_par(get_ui_id($labelInstrumentName), $CONTROL_PAR_HIDE, $HIDE_PART_BG) { Hide the label backgrounds }
        move_control_px($labelInstrumentName, 175, 300) { Position the label }
    else { If running headless then hide all the controls }
        set_control_par(get_ui_id($labelInstrumentName), $CONTROL_PAR_HIDE, $HIDE_WHOLE_CONTROL) { Hide the instrument name label }
    end if
    
    declare $i { Loop counter }
    declare $dummyNote { Variable for playing a dummy note to set the waveform display }
    declare $value { Variable for setting the scaled macro value }
    declare $touchedControl { Variable for keeping track of the last touched control }
    declare $groupSelect { Variable for keeping track of the playable group }
    declare const $numMacros := 11 { Number of NKS macros }
    declare const $groupMacros := 5 { Number of Poly Macros }
    {Effect dry wet scaling}
    declare %drywet[1001] := (0, 46419, 58484, 66479, 73299, 79042, 83758, 88264, 92351, 95879, 99377, 102644, 105529, 108441, 111205, 113839, 116201, 118620, 120943, 123181, 125207, 127299, 129323, 131287, 133076, 134932, 136628, 138391, 140110, 141685, 143326, 144832, 146404, 147944, 149451, 150838, 152289, 153713, 155111, 156400, 157751, 159080, 160386, 161592, 162859, 164029, 165259, 166396, 167591, 168770, 169933, 171008, 172140, 173258, 174293, 175384, 176461, 177459, 178512, 179487, 180516, 181533, 182477, 183473, 184458, 185432, 186337, 187233, 188179, 189116, 189986, 190905, 191816, 192662, 193556, 194386, 195264, 196081, 196944, 197800, 198649, 199437, 200220, 201048, 201870, 202634, 203442, 204195, 204991, 205782, 206517, 207296, 208021, 208741, 209503, 210260, 210964, 211711, 212406, 213142, 213874, 214555, 215232, 215949, 216662, 217326, 217985, 218684, 219379, 220027, 220671, 221353, 222032, 222664, 223292, 223959, 224581, 225199, 225854, 226506, 227113, 227718, 228359, 228957, 229551, 230183, 230771, 231395, 231977, 232556, 233171, 233745, 234354, 234921, 235487, 236086, 236646, 237202, 237794, 238345, 238894, 239476, 240021, 240598, 241101, 241673, 242207, 242774, 243303, 243830, 244389, 244911, 245431, 245949, 246499, 246978, 247523, 248032, 248574, 249045, 249581, 250082, 250614, 251077, 251605, 252098, 252589, 253077, 253564, 254082, 254565, 255047, 255526, 256004, 256480, 256954, 257427, 257929, 258398, 258865, 259331, 259795, 260257, 260718, 261177, 261634, 262120, 262544, 262997, 263448, 263927, 264346, 264792, 265267, 265710, 266123, 266563, 267003, 267440, 267877, 268311, 268745, 269177, 269608, 270037, 270465, 270891, 271288, 271712, 272136, 272556, 272977, 273396, 273813, 274229, 274617, 275030, 275443, 275854, 276237, 276646, 277054, 277460, 277866, 278243, 278647, 279049, 279449, 279822, 280221, 280619, 280988, 281384, 281778, 282145, 282537, 282928, 283318, 283681, 284069, 284430, 284816, 285201, 285558, 285941, 286298, 286653, 287033, 287412, 287765, 288141, 288492, 288868, 289242, 289590, 289937, 290309, 290654, 291000, 291368, 291735, 292078, 292420, 292784, 293125, 293488, 293826, 294164, 294525, 294860, 295195, 295530, 295887, 296220, 296552, 296907, 297238, 297567, 297897, 298248, 298576, 298903, 299229, 299554, 299902, 300227, 300550, 300872, 301194, 301516, 301836, 302179, 302498, 302794, 303134, 303451, 303768, 304083, 304398, 304713, 305027, 305340, 305653, 305965, 306276, 306586, 306897, 307206, 307493, 307801, 308109, 308416, 308722, 309028, 309333, 309638, 309920, 310223, 310527, 310828, 311109, 311410, 311710, 312010, 312309, 312587, 312885, 313183, 313459, 313755, 314051, 314326, 314621, 314894, 315189, 315481, 315754, 316046, 316317, 316609, 316878, 317169, 317458, 317727, 317995, 318283, 318550, 318837, 319104, 319390, 319655, 319920, 320205, 320469, 320753, 321016, 321279, 321541, 321822, 322084, 322345, 322625, 322885, 323145, 323404, 323663, 323941, 324199, 324456, 324714, 324971, 325227, 325483, 325758, 325994, 326268, 326522, 326775, 327029, 327282, 327535, 327787, 328039, 328291, 328542, 328793, 329024, 329274, 329524, 329773, 330022, 330270, 330518, 330766, 330995, 331242, 331488, 331716, 331962, 332207, 332452, 332697, 332923, 333167, 333411, 333636, 333879, 334104, 334346, 334588, 334811, 335053, 335275, 335516, 335738, 335979, 336200, 336439, 336660, 336899, 337119, 337339, 337577, 337796, 338033, 338252, 338470, 338706, 338924, 339142, 339359, 339594, 339811, 340028, 340244, 340477, 340693, 340908, 341123, 341338, 341552, 341766, 341981, 342212, 342425, 342621, 342851, 343064, 343276, 343470, 343682, 343894, 344123, 344317, 344527, 344737, 344948, 345158, 345367, 345560, 345769, 345978, 346187, 346396, 346586, 346794, 347002, 347193, 347400, 347589, 347796, 348003, 348192, 348398, 348604, 348793, 348981, 349186, 349391, 349579, 349766, 349970, 350157, 350361, 350548, 350734, 350937, 351124, 351310, 351512, 351697, 351899, 352084, 352269, 352454, 352655, 352822, 353023, 353207, 353390, 353574, 353757, 353940, 354123, 354306, 354489, 354671, 354853, 355051, 355216, 355398, 355579, 355761, 355941, 356122, 356303, 356483, 356648, 356827, 357007, 357187, 357366, 357530, 357709, 357888, 358051, 358229, 358408, 358570, 358748, 358910, 359088, 359266, 359427, 359588, 359765, 359942, 360103, 360264, 360440, 360601, 360761, 360937, 361097, 361257, 361432, 361591, 361751, 361910, 362084, 362243, 362402, 362561, 362735, 362893, 363051, 363210, 363368, 363525, 363683, 363840, 363998, 364155, 364312, 364469, 364626, 364782, 364939, 365079, 365236, 365392, 365548, 365704, 365859, 366000, 366155, 366310, 366465, 366605, 366760, 366900, 367055, 367209, 367363, 367502, 367641, 367795, 367934, 368088, 368226, 368379, 368518, 368671, 368809, 368962, 369100, 369237, 369375, 369528, 369665, 369802, 369939, 370092, 370228, 370365, 370502, 370639, 370775, 370911, 371047, 371199, 371320, 371456, 371606, 371742, 371863, 371998, 372134, 372269, 372404, 372540, 372675, 372810, 372930, 373065, 373199, 373334, 373453, 373588, 373722, 373842, 373976, 374095, 374228, 374347, 374482, 374600, 374734, 374853, 374986, 375105, 375238, 375356, 375474, 375607, 375725, 375844, 375976, 376094, 376212, 376329, 376462, 376579, 376697, 376814, 376932, 377049, 377167, 377283, 377400, 377532, 377635, 377751, 377868, 377985, 378102, 378218, 378335, 378451, 378553, 378669, 378785, 378902, 379003, 379119, 379235, 379336, 379452, 379568, 379683, 379785, 379900, 380002, 380117, 380218, 380333, 380434, 380549, 380649, 380750, 380865, 380966, 381066, 381181, 381281, 381381, 381482, 381596, 381696, 381797, 381897, 381997, 382096, 382197, 382296, 382396, 382496, 382610, 382695, 382794, 382894, 382994, 383093, 383193, 383292, 383377, 383476, 383575, 383675, 383773, 383859, 383957, 384056, 384141, 384240, 384324, 384423, 384522, 384606, 384705, 384789, 384888, 384972, 385057, 385155, 385239, 385323, 385422, 385506, 385590, 385674, 385772, 385856, 385940, 386024, 386108, 386205, 386289, 386373, 386457, 386540, 386624, 386708, 386791, 386875, 386958, 387028, 387111, 387195, 387278, 387361, 387445, 387514, 387598, 387681, 387750, 387833, 387916, 387986, 388068, 388151, 388221, 388290, 388372, 388456, 388524, 388594, 388676, 388745, 388814, 388897, 388966, 389035, 389117, 389186, 389255, 389324, 389392, 389475, 389544, 389612, 389680, 389749, 389818, 389886, 389955, 390023, 390092, 390160, 390228, 390297, 390352, 390420, 390489, 390557, 390625, 390680, 390748, 390816, 390871, 390939, 391007, 391062, 391130, 391184, 391252, 391307, 391375, 391429, 391497, 391551, 391606, 391674, 391728, 391783, 391850, 391905, 391959, 392013, 392067, 392135, 392189, 392244, 392297, 392352, 392406, 392460, 392514, 392568, 392622, 392677, 392730, 392784, 392825, 392879, 392933, 392987, 393041, 393082, 393135, 393189, 393230, 393283, 393337, 393378, 393431, 393472, 393526, 393566, 393620, 393661, 393714, 393755, 393795, 393848, 393889, 393929, 393983, 394023, 394063, 394104, 394144, 394197, 394238, 394278, 394318, 394358, 394398, 394439, 394479, 394519, 394559, 394599, 394640, 394679, 394706, 394747, 394786, 394827, 394853, 394894, 394934, 394960, 395001, 395040, 395067, 395107, 395134, 395174, 395200, 395241, 395267, 395307, 395334, 395361, 395401, 395427, 395468, 395494, 395520, 395548, 395574, 395614, 395640, 395667, 395694, 395720, 395747, 395774, 395800, 395827, 395853, 395880, 395907, 395933, 395960, 395986, 395999, 396026, 396053, 396079, 396092, 396119, 396145, 396159, 396186, 396199, 396225, 396252, 396265, 396292, 396305, 396331, 396344, 396358, 396385, 396398, 396411, 396438, 396451, 396464, 396477, 396504, 396517, 396530, 396544, 396557, 396570, 396583, 396596, 396610, 396623, 396636, 396649, 396663, 396676, 396689, 396702, 396702, 396716, 396729, 396742, 396742, 396755, 396768, 396768, 396782, 396782, 396795, 396809, 396809, 396821, 396821, 396821, 396835, 396835, 396848, 396848, 396848, 396848, 396848, 396848, 396848, 396848, 396849, 396849, 396849, 396849, 396849, 396849, 396850)

    {Delay Sync Variables}
    declare const $numFxSyncTimes := 20
    declare const $delayDiv := 1000000 / ($numFxSyncTimes-1)
    declare %delaySyncUnit[$numFxSyncTimes] := ($NI_SYNC_UNIT_64TH_TRIPLET, $NI_SYNC_UNIT_64TH, $NI_SYNC_UNIT_32ND_TRIPLET, $NI_SYNC_UNIT_256TH, $NI_SYNC_UNIT_32ND, $NI_SYNC_UNIT_16TH_TRIPLET, $NI_SYNC_UNIT_64TH, $NI_SYNC_UNIT_16TH, $NI_SYNC_UNIT_8TH_TRIPLET, $NI_SYNC_UNIT_32ND, $NI_SYNC_UNIT_8TH, $NI_SYNC_UNIT_QUARTER_TRIPLET, $NI_SYNC_UNIT_16TH, $NI_SYNC_UNIT_QUARTER, $NI_SYNC_UNIT_HALF_TRIPLET, $NI_SYNC_UNIT_8TH, $NI_SYNC_UNIT_HALF, $NI_SYNC_UNIT_WHOLE_TRIPLET, $NI_SYNC_UNIT_QUARTER, $NI_SYNC_UNIT_WHOLE)
  	declare !delaySyncString[$numFxSyncTimes]
	   !delaySyncString[0] := "1/64 T"
	   !delaySyncString[1] := "1/64"
	   !delaySyncString[2] := "1/32 T"
	   !delaySyncString[3] := "1/64 D"
	   !delaySyncString[4] := "1/32"
	   !delaySyncString[5] := "1/16 T"
	   !delaySyncString[6] := "1/32 D"
	   !delaySyncString[7] := "1/16"
	   !delaySyncString[8] := "1/8 T"
	   !delaySyncString[9] := "1/16 D"
	   !delaySyncString[10] := "1/8"
	   !delaySyncString[11] := "1/4 T"
	   !delaySyncString[12] := "1/8 D"
	   !delaySyncString[13] := "1/4"
	   !delaySyncString[14] := "1/2 T"
	   !delaySyncString[15] := "1/4 D"
	   !delaySyncString[16] := "1/2"
	   !delaySyncString[17] := "1/1 T"
	   !delaySyncString[18] := "1/2 D"
	   !delaySyncString[19] := "1/1"
  	declare %delaySyncValue[$numFxSyncTimes] := (0, 0, 0, 454545, 0, 0, 181818, 0, 0, 181818, 0, 0, 181818, 0, 0, 181818, 0, 0, 181818, 0) 

    { 11 NKS Macros }
    declare ui_slider $macro1 (0, $NUM_GROUPS - 1) { Source select }
    declare ui_slider $macro2 (110000, 900000) { Amp envelope attack, limit to useful ranges }
    declare ui_slider $macro3 (110000, 850000) { Amp envelope decay, limit to useful ranges }
    declare ui_slider $macro4 (0, 1000000) { Amp envelope sustain }
    declare ui_slider $macro5 (110000, 850000) { Amp envelope release, limit to useful ranges }
    declare ui_slider $macro6 (0, 1000000) { Filter cutoff }
    declare ui_slider $macro7 (0, 1000000) { Filter resonanace }
    declare ui_slider $macro8 (500000, 1000000) { Saturation amount, halfway equals no saturation so we init to that }
    declare ui_slider $macro9 (0, 1000000) { Delay Time }
    declare ui_slider $macro10 (0, 1000) { Delay Wet }
    declare ui_slider $macro11 (0, 990000) { Lofi }

    { Make all macros persistent }
    make_persistent($macro1) { Source }
    make_persistent($macro2) { Attack }
    make_persistent($macro3) { Decay }
    make_persistent($macro4) { Sustain }
    make_persistent($macro5) { Release }
    make_persistent($macro6) { Cutoff }
    make_persistent($macro7) { Resonance }
    make_persistent($macro8) { Saturation }
    make_persistent($macro9) { Time }
    make_persistent($macro10) { Wet }
    make_persistent($macro11) { Lofi }

    { Labels for each macro }
    declare ui_label $label1(1, 1) { Source }
    declare ui_label $label2(1, 1) { Attack }
    declare ui_label $label3(1, 1) { Decay }
    declare ui_label $label4(1, 1) { Sustain }
    declare ui_label $label5(1, 1) { Release }
    declare ui_label $label6(1, 1) { Cutoff }
    declare ui_label $label7(1, 1) { Resonance }
    declare ui_label $label8(1, 1) { Saturation }
    declare ui_label $label9(1, 1) { Time }
    declare ui_label $label10(1, 1) { Wet }
    declare ui_label $label11(1, 1) { Lofi }

    declare !macroText[$numMacros] { Macro Texts }
    declare !valueType[$numMacros] { Value Types }
    declare !helpText[$numMacros]  { Help Texts }
    declare %enginePar[$numMacros] := (-1, $ENGINE_PAR_ATTACK, $ENGINE_PAR_DECAY, $ENGINE_PAR_SUSTAIN, $ENGINE_PAR_RELEASE, ...
                                            $ENGINE_PAR_CUTOFF, $ENGINE_PAR_RESONANCE, $ENGINE_PAR_SHAPE, -1, -1, $ENGINE_PAR_FREQUENCY) { Engine parameters }
    declare %groupSlot[$numMacros] := (-1) { Group slot }
    declare %fxSlot[$numMacros] := (0, 0, 0, 0, 0, 0, 0, 1, 2, 2, 3) { FX slot }
    declare %genericSlot[$numMacros] := (-1, -1, -1, -1, -1, 1, 1, 1, 1, 1, 1) { Generic slot }

    !macroText[0] := "Source"
    !macroText[1] := "Attack"
    !macroText[2] := "Decay"
    !macroText[3] := "Sustain"
    !macroText[4] := "Release"
    !macroText[5] := "Cutoff"
    !macroText[6] := "Res"
    !macroText[7] := "Saturate"
    !macroText[8] := "Time"
    !macroText[9] := "Wet"
    !macroText[10] := "Lofi"

    !valueType[0] := "" { No value type for source }
    !valueType[1] := " ms" { Attack in ms }
    !valueType[2] := " ms" { Decay in ms }
    !valueType[3] := " dB" { Sustain in dB }
    !valueType[4] := " ms" { Release in ms }
    !valueType[5] := " Hz" { Cutoff in Hz }
    !valueType[6] := " %" { Resonance in % }
    !valueType[7] := " %" { Saturation in % }
    !valueType[8] := "" { Time in string }
    !valueType[9] := " %" { Wet in %}
    !valueType[10] := " Hz" { Lofi in Hz}

    !helpText[0] := "Selects the playable source group"
    !helpText[1] := "Amplitude envelope attack time"
    !helpText[2] := "Amplitude envelope decay time"
    !helpText[3] := "Amplitude envelope sustain level"
    !helpText[4] := "Amplitude envelope release time"
    !helpText[5] := "Sets the cutoff point of the lowpass filter"
    !helpText[6] := "Sets the resonance amount of the lowpass filter"
    !helpText[7] := "Amount of saturation applied to the signal"
    !helpText[8] := "Sets the synced time of the delay effect"
    !helpText[9] := "Amount of delay effect mixed with the dry signal"
    !helpText[10] := "Amount of sample rate reduction applied to the signal"

    declare %macroID[$numMacros] { Array for the control GUI IDs }
    declare %labelID[$numMacros] { Array for the label GUI IDs }

    declare ui_waveform $waveformDisplay (1, 1) { Declare the waveform display }
    declare ui_xy ?xyPad[2] { Declare the XY pad }
    declare $waveformID := get_ui_id($waveformDisplay) { Get the waveform display ID }
    declare $xyID := get_ui_id(?xyPad) { Get the XY pad ID }

    declare const $guiOffsetX   := 150 { GUI main offset x }
    declare const $guiOffsetY   := 10  { GUI main offset y }
    declare const $guiSpacingX  := 100 { GUI element spacing x }
    declare const $guiSpacingY  := 40  { GUI element spacing y }
    declare const $labelOffsetY := 20  { Label spacing offset }
    declare const $displaySize  := 185 { Size for the waveform display and XY squares }

    $i := 0 { Set the control parameters }
    while($i < $numMacros) { Run the loop for the mumber of macros }
        %macroID[$i] := get_ui_id($macro1) + $i { Put the controls in the GUI ID array }
        %labelID[$i] := get_ui_id($label1) + $i { Put the labels in the GUI ID array }

        set_control_par(%macroID[$i], $CONTROL_PAR_AUTOMATION_ID, $i) { Set the automation IDs }
        set_control_par_str(%macroID[$i], $CONTROL_PAR_AUTOMATION_NAME, !macroText[$i]) { Set the automation names }
        set_control_par_str(%macroID[$i], $CONTROL_PAR_HELP, !helpText[$i]) { Set the help texts }
        set_control_par_str(%macroID[$i], $CONTROL_PAR_PICTURE, "knob") { Set the picture }
        set_control_par(%macroID[$i], $CONTROL_PAR_MOUSE_BEHAVIOUR, -2000) { Set the automation IDs }
        set_control_par(%labelID[$i], $CONTROL_PAR_HIDE, $HIDE_PART_BG) { Hide the label backgrounds }

        if ($NI_KONTAKT_IS_HEADLESS = 0) { Check if running on headless }
            select ($i)
                case 0 { Position source select macro }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_X, $guiOffsetX / 3) { Control X }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY) { Control Y }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_X, $guiOffsetX / 3) { Label X }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY + $guiSpacingY + $labelOffsetY) { Label Y }

                    { Use this single case to also position the waveform display and XY pad }
                    set_control_par($waveformID, $CONTROL_PAR_BG_COLOR, 0000000h) { Prepare and position the waveform display }
                    set_control_par($waveformID, $CONTROL_PAR_HEIGHT, $displaySize) { Set the height of the waveform display }
                    set_control_par($waveformID, $CONTROL_PAR_WIDTH, $displaySize) { Set the width of the waveform display}
                    set_control_par($waveformID, $CONTROL_PAR_POS_X, $guiOffsetX) { Position the waveform display X }
                    set_control_par($waveformID, $CONTROL_PAR_POS_Y, ($guiOffsetY * 8) + $labelOffsetY) { Position the waveform display Y }

                    set_control_par_str($xyID, $CONTROL_PAR_CURSOR_PICTURE, "cursor") { Prepare and position the XY pad }
                    set_control_par($xyID, $CONTROL_PAR_MOUSE_MODE, 2) { Set the XY pad to capture clicks anywhere and not just on the cursor }
                    set_control_par($xyID, $CONTROL_PAR_HEIGHT, $displaySize) { Set the high of the XY pad }
                    set_control_par($xyID, $CONTROL_PAR_WIDTH, $displaySize) {Set the wifth of the XY pad }
                    set_control_par($xyID, $CONTROL_PAR_POS_X, $guiOffsetX + 200) { Position the XY pad X }
                    set_control_par($xyID, $CONTROL_PAR_POS_Y, ($guiOffsetY * 8) + $labelOffsetY) { Position the XY pad Y }
                case 1 to 4 { Position ADSR macros }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_X, $guiOffsetX + (($i - 1) * $guiSpacingX)) { Control X }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY) { Control Y }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_X, $guiOffsetX + (($i - 1) * $guiSpacingX)) { Label X }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY + $guiSpacingY + $labelOffsetY) { Label Y }
                case 5 to 7 { Position cut, res and saturate macros }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_X, $guiOffsetX / 3) { Control X }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY + $guiSpacingY * ((2 * $i) - 8)) { Control Y }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_X, $guiOffsetX / 3) { Label X }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY + ($guiSpacingY * ((2 * $i) - 7) + $labelOffsetY)) { Label Y }
                case 8 to 10 { Position time, wet and lofi }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_X, ($guiOffsetX * 3) + $guiSpacingX) { Control X }
                    set_control_par(%macroID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY + $guiSpacingY * ((2 * $i) - 14)) { Control Y }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_X, ($guiOffsetX * 3) + $guiSpacingX) { Label X }
                    set_control_par(%labelID[$i], $CONTROL_PAR_POS_Y, $guiOffsetY + ($guiSpacingY * ((2 * $i) - 13) + $labelOffsetY)) { Label Y }
             end select
        else { Hide all the controls }
            set_control_par(%macroID[$i], $CONTROL_PAR_HIDE, $HIDE_WHOLE_CONTROL) { Macros }
            set_control_par(%labelID[$i], $CONTROL_PAR_HIDE, $HIDE_WHOLE_CONTROL) { Labels }
            set_control_par($waveformID, $CONTROL_PAR_HIDE, $HIDE_WHOLE_CONTROL) { Waveform display }
            set_control_par($xyID, $CONTROL_PAR_HIDE, $HIDE_WHOLE_CONTROL) { XY Pad }
        end if

        inc($i)
    end while

    set_engine_par($ENGINE_PAR_EFFECT_TYPE, $EFFECT_TYPE_FILTER, -1, 0, 1) { Place a filter in the first insert slot }
    set_engine_par($ENGINE_PAR_EFFECT_SUBTYPE, $FILTER_TYPE_SV_LP4, -1, 0, 1) { Set the filter to 4-pole SV lowpass }
    set_engine_par($ENGINE_PAR_EFFECT_TYPE, $EFFECT_TYPE_SHAPER, -1, 1, 1) { Place a saturator in the second insert slot }
    set_engine_par($ENGINE_PAR_EFFECT_TYPE, $EFFECT_TYPE_DELAY, -1, 2, 1) { Place a delay in the third insert slot }
    set_engine_par($ENGINE_PAR_EFFECT_TYPE, $EFFECT_TYPE_LOFI, -1, 3, 1) { Place a lofi in the fourth insert slot }
    set_engine_par($ENGINE_PAR_EFFECT_TYPE, $EFFECT_TYPE_LIMITER, -1, 7, 2) { Place a limiter on the last main effect slot }
    
    set_control_par(%macroID[3], $CONTROL_PAR_DEFAULT_VALUE, 1000000) { Sustain default value }
    set_control_par(%macroID[3], $CONTROL_PAR_VALUE, 1000000) { Sustain init to default value }
    set_control_par(%macroID[5], $CONTROL_PAR_DEFAULT_VALUE, 1000000) { Filter cutoff default value }
    set_control_par(%macroID[5], $CONTROL_PAR_VALUE, 1000000) { Filter cutoff init to default value }
end on

function setMacro { Set the macros }
    if ($touchedControl < $groupMacros) { Check if a touched control is on group or instrument level }
        %groupSlot[$touchedControl] := $groupSelect { If the control is on a group level,  set the group slot }
    end if

    { Set the engine parameter }
    set_engine_par(%enginePar[$touchedControl], ...
                   get_control_par(%macroID[$touchedControl],  $CONTROL_PAR_VALUE), ...
                   %groupSlot[$touchedControl], ...
                   %fxSlot[$touchedControl], ...
                   %genericSlot[$touchedControl])

    { Update the internal slider label for host automation }
    set_control_par_str(%macroID[$touchedControl], $CONTROL_PAR_LABEL, ...
                        get_engine_par_disp(%enginePar[$touchedControl], ...
                                            %groupSlot[$touchedControl], ...
                                            %fxSlot[$touchedControl], ...
                                            %genericSlot[$touchedControl]) & !valueType[$touchedControl]) 

    { Update the GUI label }
    if ($NI_KONTAKT_IS_HEADLESS = 0)
        set_control_par_str(%labelID[$touchedControl], $CONTROL_PAR_TEXT, ...
                            !macroText[$touchedControl] & ": " & ...
                            get_engine_par_disp(%enginePar[$touchedControl], ...
                                                %groupSlot[$touchedControl], ...
                                                %fxSlot[$touchedControl], ...
                                                %genericSlot[$touchedControl]) & !valueType[$touchedControl])
    end if
end function

function setWet {Set dry/wet controls}
    set_engine_par($ENGINE_PAR_SEND_EFFECT_OUTPUT_GAIN,%drywet[get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE)],%groupSlot[$touchedControl],%fxSlot[$touchedControl],%genericSlot[$touchedControl])
    set_engine_par($ENGINE_PAR_SEND_EFFECT_DRY_LEVEL,%drywet[1000 - get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE)],%groupSlot[$touchedControl],%fxSlot[$touchedControl],%genericSlot[$touchedControl])
    {Update the internal slider label for host automation}
    set_control_par_str(%macroID[$touchedControl],$CONTROL_PAR_LABEL,%drywet[get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE)] & !valueType[$touchedControl])
    {Update the GUI label}
    if($NI_KONTAKT_IS_HEADLESS=0)
        set_control_par_str(%labelID[$touchedControl],$CONTROL_PAR_TEXT,!macroText[$touchedControl] & ": " & real_to_int(100.0 * (int_to_real(get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE)) / 1001.0))  & !valueType[$touchedControl])
    end if
end function

function setDelayTime {Set synced delay time controls}
    {Set the engine parameter}
    set_engine_par($ENGINE_PAR_DL_TIME_UNIT,%delaySyncUnit[get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE) / $delayDiv],%groupSlot[$touchedControl],%fxSlot[$touchedControl],%genericSlot[$touchedControl])
    set_engine_par($ENGINE_PAR_DL_TIME,%delaySyncUnit[get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE) / $delayDiv],%groupSlot[$touchedControl],%fxSlot[$touchedControl],%genericSlot[$touchedControl])
    {Update the internal slider label for host automation}
    set_control_par_str(%macroID[$touchedControl],$CONTROL_PAR_LABEL,!delaySyncString[get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE) / $delayDiv])
    {Update the GUI label}
    if($NI_KONTAKT_IS_HEADLESS=0)
        set_control_par_str(%labelID[$touchedControl],$CONTROL_PAR_TEXT,!macroText[$touchedControl] & ": " & !delaySyncString[get_control_par(%macroID[$touchedControl],$CONTROL_PAR_VALUE) / $delayDiv])
    end if
end function

function setWaveformDisplay { Update the waveform display with a wave from the selected group }
    $dummyNote := play_note(48, 127, 0, 10) { We have to play a dummy note to get the zone ID }
    change_vol($dummyNote, -200000, 0) { We don't want to hear this note }
    set_event_par_arr($dummyNote,$EVENT_PAR_ALLOW_GROUP, 0, $ALL_GROUPS) { Disallow all groups }
    set_event_par_arr($dummyNote,$EVENT_PAR_ALLOW_GROUP, 1, $groupSelect) { Play the note in the selected group }
    wait(1) { Wait for the note to exist to attach it }
    attach_zone($waveformDisplay, get_event_par($dummyNote, $EVENT_PAR_ZONE_ID), 0) { Attach the last played zone to the Waveform view }
end function

function setGroupSelect { Set the selected group }
    $groupSelect := $macro1 { Selected group set by first macro }

    set_control_par_str(%macroID[$touchedControl], $CONTROL_PAR_LABEL, $groupSelect + 1) { Update the internal slider label for host automation }

    if ($NI_KONTAKT_IS_HEADLESS = 0) { Update the GUI label }
        set_control_par_str(%labelID[$touchedControl], $CONTROL_PAR_TEXT, !macroText[0] & ": " & $groupSelect + 1)
    end if

    $i := 1  { Set the macro values for the selected group }
    while ($i < $numMacros) { Loop to set all macros }
        $touchedControl := $i
        call setMacro

        inc($i)
    end while

    call setWaveformDisplay { Call the function to set the waveform display }
end function

on persistence_changed { executed after the init callback or whenever a snapshot has been loaded }
    call setGroupSelect { Set the source group and update all parameters }
    ?xyPad[0] := int_to_real($macro6) / 1000000.0 { Update XY pad X position }
    ?xyPad[1] := int_to_real($macro7) / 1000000.0 { Update XY pad Y position }
    $touchedControl := 8
    call setDelayTime
    $touchedControl := 9
    call setWet
end on

on note { Note logic }
    disallow_group($ALL_GROUPS) { First disallow all groups }
    allow_group($groupSelect) { Play only the selected group }
end on

on ui_control (?xyPad) { XY Pad }
    $macro6 := real_to_int(?xyPad[0] * 1000000.0) { Set the filter cutoff to X value }
    $touchedControl := 5
    call setMacro

    $macro7 := real_to_int(?xyPad[1] * 1000000.0) { Set the filter resonanace to Y value }
    $touchedControl := 6
    call setMacro
end on

on ui_control ($macro1) { Group Select }
    $touchedControl := 0
    call setGroupSelect { In this case call the group select function and not the macro function }
end on

on ui_control ($macro2) { Amp Attack }
    $touchedControl := 1
    call setMacro
end on

on ui_control ($macro3) { Amp Decay }
    $touchedControl := 2
    call setMacro
end on

on ui_control ($macro4) { Amp Sustain }
    $touchedControl := 3
    call setMacro
end on

on ui_control ($macro5) { Amp Release }
    $touchedControl := 4
    call setMacro
end on

on ui_control ($macro6) { Filter Cut }
    ?xyPad[0] := int_to_real($macro6) / 1000000.0 { Set the XY Pad X }
    $touchedControl := 5
    call setMacro
end on

on ui_control ($macro7) { Filter Res }
    ?xyPad[1] := int_to_real($macro7) / 1000000.0 { Set the XY Pad Y }
    $touchedControl := 6
    call setMacro
end on

on ui_control ($macro8) { Saturation Amount }
    $touchedControl := 7
    call setMacro
end on

on ui_control ($macro9) { Delay Time }
    $touchedControl := 8
    call setDelayTime
end on

on ui_control ($macro10) { Delay Wet }
    $touchedControl := 9
    call setWet
end on

on ui_control ($macro11) { Lofi }
    $touchedControl := 10
    call setMacro
end on