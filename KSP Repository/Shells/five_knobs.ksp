{***********************************************
Five Knobs
Author: Native Instruments
Written by: Yaron Eshkar
Modified: April 20, 2022
*************************************************}

on init

    declare const $x_spacing := 100
    declare const $y_spacing := 50
    declare const $panel_x := 75
    declare const $panel_y := 50

    declare const $num_controls := 5
    declare const $group_macros := 4

    declare @background
    declare @icon
    @background := "background"
    @icon := "icon" 

    message("")
    set_script_title("Instrument")
    make_perfview
    set_ui_height(5)

    set_ui_color(9000000h) {can remove if background exists}

    set_control_par_str ($INST_WALLPAPER_ID,$CONTROL_PAR_PICTURE,@background)
	set_control_par_str($INST_ICON_ID,$CONTROL_PAR_PICTURE,@icon)

    declare $i
    declare $touched

    declare $timer := 10000
	declare $pgs_counter
	pgs_create_key(CONTROLS, 1)
	pgs_set_key_val(CONTROLS, 0, 0)

    declare ui_panel $macros
    declare ui_slider $macro1(0,1000000)
    declare ui_slider $macro2(0,1000000)
    declare ui_slider $macro3(0,1000000)
    declare ui_slider $macro4(0,1000000)
    declare ui_slider $macro5(0,1000000)

    declare ui_label $label1(1,1)
    declare ui_label $label2(1,1)
    declare ui_label $label3(1,1)
    declare ui_label $label4(1,1)
    declare ui_label $label5(1,1)

    make_persistent($macro1)
    make_persistent($macro2)
    make_persistent($macro3)
    make_persistent($macro4)
    make_persistent($macro5)

    declare %engine_pars[$num_controls]
        %engine_pars[0] := $ENGINE_PAR_ATTACK
        %engine_pars[1] := $ENGINE_PAR_DECAY
        %engine_pars[2] := $ENGINE_PAR_SUSTAIN
        %engine_pars[3] := $ENGINE_PAR_RELEASE
        %engine_pars[4] := $ENGINE_PAR_CUTOFF

    declare %group[$num_controls]
        %group[0] := 0
        %group[1] := 0
        %group[2] := 0
        %group[3] := 0
        %group[4] := -1

    declare %slot[$num_controls]
        %slot[0] := 0
        %slot[1] := 0
        %slot[2] := 0
        %slot[3] := 0
        %slot[4] := 0

    declare %generic[$num_controls]
        %generic[0] := 0
        %generic[1] := 0
        %generic[2] := 0
        %generic[3] := 0
        %generic[4] := 1

    declare !label_text[$num_controls]
        !label_text[0] := "Attack"
        !label_text[1] := "Decay"
        !label_text[2] := "Sustain"
        !label_text[3] := "Release"
        !label_text[4] := "Cut"

    declare !value_type[$num_controls]
        !value_type[0] := "ms"
        !value_type[1] := "ms"
        !value_type[2] := "dB"
        !value_type[3] := "ms"
        !value_type[4] := "Hz"    

    declare %mouse_behaviour[$num_controls]
        %mouse_behaviour[0] := 2000
        %mouse_behaviour[1] := 2000
        %mouse_behaviour[2] := 2000
        %mouse_behaviour[3] := 2000
        %mouse_behaviour[4] := 2000

    declare %controlID[$num_controls*2]

    set_engine_par($ENGINE_PAR_EFFECT_TYPE,$EFFECT_TYPE_FILTER,-1,0,1)
    set_engine_par($ENGINE_PAR_EFFECT_SUBTYPE,$FILTER_TYPE_SV_LP4,-1,0,1)

    $i := 0
    while($i<num_elements(%controlID))
        %controlID[$i] := get_ui_id($macro1)+$i
        set_control_par(%controlID[$i],$CONTROL_PAR_PARENT_PANEL,get_ui_id($macros))
        if($i<$num_controls)
            set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$x_spacing*$i)
            set_control_par_str(%controlID[$i],$CONTROL_PAR_PICTURE,"knob")
            set_control_par(%controlID[$i],$CONTROL_PAR_MOUSE_BEHAVIOUR,%mouse_behaviour[$i])
            set_control_par(%controlID[$i],$CONTROL_PAR_AUTOMATION_ID,$i)
            set_control_par_str(%controlID[$i],$CONTROL_PAR_AUTOMATION_NAME,!label_text[$i])
        else
            set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$x_spacing*($i-$num_controls))
            set_control_par(%controlID[$i],$CONTROL_PAR_POS_Y,$y_spacing)
            set_control_par(%controlID[$i],$CONTROL_PAR_TEXT_ALIGNMENT,1)
            set_control_par(%controlID[$i],$CONTROL_PAR_HIDE,$HIDE_PART_BG)
            set_control_par_str(%controlID[$i],$CONTROL_PAR_TEXT,!label_text[$i-$num_controls])
        end if
        inc($i)
    end while

    set_control_par(get_ui_id($macros),$CONTROL_PAR_POS_X,$panel_x)
    set_control_par(get_ui_id($macros),$CONTROL_PAR_POS_Y,$panel_y)
end on

function pgs_control
  if(pgs_get_key_val(CONTROLS, 0) = 0)
    $pgs_counter := 100
    pgs_set_key_val(CONTROLS, 0, 1)
  end if
end function

on pgs_changed
  if(pgs_key_exists(CONTROLS) and pgs_get_key_val(CONTROLS, 0) = 1)
    while ($pgs_counter > 0)
      wait($timer)
      dec($pgs_counter)
    end while
  end if
  $i := 0
  while ($i<$num_controls)
    set_control_par_str(%controlID[$i+$num_controls],$CONTROL_PAR_TEXT,!label_text[$i])
    inc($i)
  end while
  pgs_set_key_val(CONTROLS, 0,0)
end on

function set_controls
    if ($touched<$group_macros)
        $i := 0
        while($i<$NUM_GROUPS)
            set_engine_par(%engine_pars[$touched],get_control_par(%controlID[$touched],$CONTROL_PAR_VALUE),$i,%slot[$touched],%generic[$touched])
            inc($i)
        end while
    else
        set_engine_par(%engine_pars[$touched],get_control_par(%controlID[$touched],$CONTROL_PAR_VALUE),%group[$touched],%slot[$touched],%generic[$touched])
    end if    
    set_control_par_str(%controlID[$touched],$CONTROL_PAR_LABEL,get_engine_par_disp(%engine_pars[$touched],%group[$touched],%slot[$touched],%generic[$touched]) & !value_type[$touched])
    set_control_par_str(%controlID[$touched+$num_controls],$CONTROL_PAR_TEXT,get_engine_par_disp(%engine_pars[$touched],%group[$touched],%slot[$touched],%generic[$touched]) & !value_type[$touched])
end function
 
on persistence_changed
    $i := 0
    while($i<$num_controls)
        $touched := $i
        call set_controls
        inc($i)
    end while        
end on

on ui_control($macro1)
    $touched := 0
    call set_controls
    call pgs_control
end on

on ui_control($macro2)
    $touched := 1
    call set_controls
    call pgs_control
end on

on ui_control($macro3)
    $touched := 2
    call set_controls
    call pgs_control
end on

on ui_control($macro4)
    $touched := 3
    call set_controls
    call pgs_control
end on

on ui_control($macro5)
    $touched := 4
    call set_controls
    call pgs_control
end on