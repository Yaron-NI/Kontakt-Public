{***********************************************
Kick Synth Simple
Author: Native Instruments
Written by: Yaron Eshkar
Modified: October 19, 2022
*************************************************}

{AHD volume envelope, AHD Pitch envelope, 3x mod targent intensity for the pitch}
{Saturator on drum mode, Limiter with default settings on instrument level}

on init
    make_perfview
    message("")
    set_script_title("Kick Synth")
    set_ui_height_px(100)
    set_ui_color(9000000h)

    declare const $num_controls := 8
    declare const $control_width := 100
    declare const $control_height := 20

    declare $i

    declare ui_label $main_label(1,1)
        set_text($main_label,"Kick Synth Simple")
    declare ui_label $amp_label(1,1)
        set_text($amp_label,"Amp")
    declare ui_label $pitch_label(1,1)
        set_text($pitch_label,"Pitch")
    declare ui_label $effect_label(1,1)
        set_text($effect_label,"Saturate")

    declare ui_panel $controls_panel
    declare ui_panel $sliders_panel
    declare ui_panel $labels_panel

    declare ui_slider $amp_decay_slider(0,600000)
    declare ui_slider $pitch_decay_slider(0,600000)
    declare ui_slider $pitch_depth_slider(0,1000000)
    declare ui_slider $saturation_slider(0,1000000)
    declare ui_label $amp_decay_label(1,1)
    declare ui_label $pitch_decay_label(1,1)
    declare ui_label $pitch_depth_label(1,1)
    declare ui_label $saturation_label(1,1)

    declare %control_ids[$num_controls]

    $i := 0
    while($i<$num_controls)
        %control_ids[$i] := get_ui_id($amp_decay_slider)+$i
        set_control_par(%control_ids[$i],$CONTROL_PAR_WIDTH,$control_width)
        set_control_par(%control_ids[$i],$CONTROL_PAR_HEIGHT,$control_height)
        if($i<$num_controls/2)
            set_control_par(%control_ids[$i],$CONTROL_PAR_PARENT_PANEL,get_ui_id($sliders_panel))
            set_control_par(%control_ids[$i],$CONTROL_PAR_POS_X,$control_width*$i)
            set_control_par(%control_ids[$i],$CONTROL_PAR_AUTOMATION_ID,$i)
        else
            set_control_par(%control_ids[$i],$CONTROL_PAR_PARENT_PANEL,get_ui_id($labels_panel))
            set_control_par(%control_ids[$i],$CONTROL_PAR_POS_X,$control_width*($i-($num_controls/2)))
        end if
        inc($i)
    end while

    set_control_par_str(get_ui_id($amp_decay_slider),$CONTROL_PAR_AUTOMATION_NAME,"Amp Decay")
    set_control_par_str(get_ui_id($pitch_decay_slider),$CONTROL_PAR_AUTOMATION_NAME,"Pitch Decay")
    set_control_par_str(get_ui_id($pitch_depth_slider),$CONTROL_PAR_AUTOMATION_NAME,"Pitch Depth")
    set_control_par_str(get_ui_id($saturation_slider),$CONTROL_PAR_AUTOMATION_NAME,"Warm")

    set_control_par(get_ui_id($sliders_panel),$CONTROL_PAR_PARENT_PANEL,get_ui_id($controls_panel))
    set_control_par(get_ui_id($labels_panel),$CONTROL_PAR_PARENT_PANEL,get_ui_id($controls_panel))

    make_persistent($amp_decay_slider)
    make_persistent($pitch_decay_slider)
    make_persistent($pitch_depth_slider)
    make_persistent($saturation_slider)

    move_control_px($main_label,250,10)
    move_control_px($amp_label,100,30)
    move_control_px($pitch_label,200,30)
    move_control_px($effect_label,400,30)
    move_control_px($controls_panel,100,50)
    move_control_px($labels_panel,0,20)
    move_control_px($sliders_panel,0,0)

end on

function set_amp_decay_text
    set_text($amp_decay_label,"Decay: " & get_engine_par_disp($ENGINE_PAR_DECAY,0,0,-1) & "ms")
    set_control_par_str(get_ui_id($amp_decay_slider),$CONTROL_PAR_LABEL,get_engine_par_disp($ENGINE_PAR_DECAY,0,0,-1) & "ms" )
end function

function set_amp_decay
    set_engine_par($ENGINE_PAR_DECAY,$amp_decay_slider,0,0,-1)
    call set_amp_decay_text
end function

function set_pitch_decay_text
    set_text($pitch_decay_label,"Decay: " & get_engine_par_disp($ENGINE_PAR_DECAY,0,1,-1) & "ms")
    set_control_par_str(get_ui_id($pitch_decay_slider),$CONTROL_PAR_LABEL,get_engine_par_disp($ENGINE_PAR_DECAY,0,1,-1) & "ms" )
end function

function set_pitch_decay
    set_engine_par($ENGINE_PAR_DECAY,$pitch_decay_slider,0,1,-1)
    call set_pitch_decay_text
end function

function set_pitch_depth_text
    set_text($pitch_depth_label,"Depth: " & $pitch_depth_slider/10000 & "%")
    set_control_par_str(get_ui_id($pitch_depth_slider),$CONTROL_PAR_LABEL,$pitch_depth_slider/10000 & "%")
end function

function set_pitch_depth
    set_engine_par($ENGINE_PAR_MOD_TARGET_INTENSITY,$pitch_depth_slider,0,1,0)
    set_engine_par($ENGINE_PAR_MOD_TARGET_INTENSITY,$pitch_depth_slider,0,1,1)
    set_engine_par($ENGINE_PAR_MOD_TARGET_INTENSITY,$pitch_depth_slider,0,1,2)
    call set_pitch_depth_text
end function

function set_saturation_text
    set_text($saturation_label,"Warm: " & get_engine_par_disp($ENGINE_PAR_SHAPE,-1,0,1) & "%")
    set_control_par_str(get_ui_id($saturation_slider),$CONTROL_PAR_LABEL,get_engine_par_disp($ENGINE_PAR_SHAPE,-1,0,1) & "%" )
end function

function set_saturation
    set_engine_par($ENGINE_PAR_SHAPE,$saturation_slider,-1,0,1)
    call set_saturation_text
end function

on persistence_changed
    call set_amp_decay_text
    call set_pitch_decay_text
    call set_pitch_depth_text
    call set_saturation_text
end on

on ui_control($amp_decay_slider)
    call set_amp_decay
end on

on ui_control($pitch_decay_slider)
    call set_pitch_decay
end on

on ui_control($pitch_depth_slider)
    call set_pitch_depth
end on

on ui_control($saturation_slider)
    call set_saturation
end on