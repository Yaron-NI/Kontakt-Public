{***********************************************
Psyche Delay Test
Author: Native Instruments
Written by: Yaron Eshkar
Modified: May 09, 2022
*************************************************}

on init

    message("")
    set_script_title("Psyche Delay Test")
    set_ui_height(3)
    make_perfview

    declare const $spacing_x := 100
    declare const $spacing_y := 20

    declare const $num_controls := 15

    declare const $group := -1
    declare const $slot := 0
    declare const $generic := 1

    declare $i

    set_engine_par($ENGINE_PAR_EFFECT_TYPE,$EFFECT_TYPE_PSYCHEDELAY,$group,$slot,$generic)

    declare ui_panel $controls

    declare ui_slider $time(0,1000000)
    declare ui_slider $feedback(0,1000000)
    declare ui_slider $pitch(0,1000000)
    declare ui_slider $offset(0,1000000)
    declare ui_slider $cross(0,1000000)
    declare ui_slider $detune(0,1000000)

    declare ui_label $time_label(1,1)
    declare ui_label $feedback_label(1,1)
    declare ui_label $pitch_label(1,1)
    declare ui_label $offset_label(1,1)
    declare ui_label $cross_label(1,1)
    declare ui_label $detune_label(1,1)

    declare ui_switch $reverse
        set_text($reverse,"Reverse")
    declare ui_switch $sreverse
        set_text($sreverse,"S Reverse")
    declare ui_switch $sdetune
        set_text($sdetune,"S Detune")

    make_persistent($time)
    make_persistent($feedback)
    make_persistent($pitch)
    make_persistent($offset)
    make_persistent($cross)
    make_persistent($detune)
    make_persistent($reverse)
    make_persistent($sreverse)
    make_persistent($sdetune)

    declare %controlID[15]

    $i := 0
    while($i<$num_controls)
        %controlID[$i] := get_ui_id($time)+$i
        set_control_par(%controlID[$i],$CONTROL_PAR_PARENT_PANEL,get_ui_id($controls))
        select ($i)
            case 0 to 5
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$spacing_x * $i)
            case 6 to 11
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$spacing_x * ($i-6))
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_Y,$spacing_y)
            case 12 to $num_controls   
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$spacing_x * ($i-11))
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_Y,$spacing_y*2)
        end select
        inc($i)
    end while

    set_control_par(get_ui_id($controls),$CONTROL_PAR_POS_X,25)
    set_control_par(get_ui_id($controls),$CONTROL_PAR_POS_Y,50)

end on


on persistence_changed
    set_text($time_label,get_engine_par_disp($ENGINE_PAR_PSYDL_TIME,$group,$slot,$generic) & "ms")
    set_text($feedback_label,get_engine_par_disp($ENGINE_PAR_PSYDL_FEEDBACK,$group,$slot,$generic) & "%")
    set_text($pitch_label,get_engine_par_disp($ENGINE_PAR_PSYDL_PITCH,$group,$slot,$generic) & "st")
    set_text($offset_label,get_engine_par_disp($ENGINE_PAR_PSYDL_LR_OFFSET,$group,$slot,$generic) & "%")
    set_text($cross_label,get_engine_par_disp($ENGINE_PAR_PSYDL_CROSS_FEEDBACK,$group,$slot,$generic) & "%")
    set_text($detune_label,get_engine_par_disp($ENGINE_PAR_PSYDL_DETUNE,$group,$slot,$generic) & "ct")
end on

on ui_control($time)
    set_engine_par($ENGINE_PAR_PSYDL_TIME,$time,$group,$slot,$generic)
    set_text($time_label,get_engine_par_disp($ENGINE_PAR_PSYDL_TIME,$group,$slot,$generic) & "ms")
end on

on ui_control($feedback)
    set_engine_par($ENGINE_PAR_PSYDL_FEEDBACK,$feedback,$group,$slot,$generic)
    set_text($feedback_label,get_engine_par_disp($ENGINE_PAR_PSYDL_FEEDBACK,$group,$slot,$generic) & "%")
end on

on ui_control($pitch)
    set_engine_par($ENGINE_PAR_PSYDL_PITCH,$pitch,$group,$slot,$generic)
    set_text($pitch_label,get_engine_par_disp($ENGINE_PAR_PSYDL_PITCH,$group,$slot,$generic) & "st")
end on

on ui_control($offset)
    set_engine_par($ENGINE_PAR_PSYDL_LR_OFFSET,$offset,$group,$slot,$generic)
    set_text($offset_label,get_engine_par_disp($ENGINE_PAR_PSYDL_LR_OFFSET,$group,$slot,$generic) & "%")
end on

on ui_control($cross)
    set_engine_par($ENGINE_PAR_PSYDL_CROSS_FEEDBACK,$cross,$group,$slot,$generic)
    set_text($cross_label,get_engine_par_disp($ENGINE_PAR_PSYDL_CROSS_FEEDBACK,$group,$slot,$generic) & "%")
end on

on ui_control($detune)
    set_engine_par($ENGINE_PAR_PSYDL_DETUNE,$detune,$group,$slot,$generic)
    set_text($detune_label,get_engine_par_disp($ENGINE_PAR_PSYDL_DETUNE,$group,$slot,$generic) & "ct")
end on

on ui_control($reverse)
    set_engine_par($ENGINE_PAR_PSYDL_REVERSE,$reverse,$group,$slot,$generic)
end on

on ui_control($sreverse)
    set_engine_par($ENGINE_PAR_PSYDL_REVERSE_STEREO,$sreverse,$group,$slot,$generic)
end on

on ui_control($sdetune)
    set_engine_par($ENGINE_PAR_PSYDL_DETUNE_STEREO,$sdetune,$group,$slot,$generic)
end on