{***********************************************
Ringmod Test
Author: Native Instruments
Written by: Yaron Eshkar
Modified: June 14, 2022
*************************************************}

on init

    message("")
    set_script_title("Ringmod Test")
    set_ui_height(3)
    make_perfview

    declare const $spacing_x := 100
    declare const $spacing_y := 20

    declare const $num_controls := 14

    declare const $group := -1
    declare const $slot := 0
    declare const $generic := 1

    declare $i

    set_engine_par($ENGINE_PAR_EFFECT_TYPE,$EFFECT_TYPE_RINGMOD,$group,$slot,$generic)

    declare ui_panel $controls

    declare ui_slider $ring(0,1000000)
    declare ui_slider $fm(0,1000000)
    declare ui_slider $freq(0,1000000)
    declare ui_slider $edge(0,1000000)
    declare ui_slider $amount(0,1000000)
    declare ui_slider $rate(0,1000000)

    declare ui_label $ring_label(1,1)
    declare ui_label $fm_label(1,1)
    declare ui_label $freq_label(1,1)
    declare ui_label $edge_label(1,1)
    declare ui_label $amount_label(1,1)
    declare ui_label $rate_label(1,1)

    declare ui_switch $fast
        set_text($fast,"Fast")
    declare ui_menu $wave
        add_menu_item($wave,"Sine",$NI_RINGMOD_LFO_WAVE_SINE)
        add_menu_item($wave,"Square",$NI_RINGMOD_LFO_WAVE_SQUARE)

    make_persistent($ring)
    make_persistent($fm)
    make_persistent($freq)
    make_persistent($edge)
    make_persistent($amount)
    make_persistent($rate)
    make_persistent($fast)
    make_persistent($wave)

    declare %controlID[15]

    $i := 0
    while($i<$num_controls)
        %controlID[$i] := get_ui_id($ring)+$i
        set_control_par(%controlID[$i],$CONTROL_PAR_PARENT_PANEL,get_ui_id($controls))
        select ($i)
            case 0 to 5
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$spacing_x * $i)
            case 6 to 11
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$spacing_x * ($i-6))
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_Y,$spacing_y)
            case 12 to $num_controls   
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_X,$spacing_x * ($i-11))
                set_control_par(%controlID[$i],$CONTROL_PAR_POS_Y,$spacing_y*2)
        end select
        inc($i)
    end while

    set_control_par(get_ui_id($controls),$CONTROL_PAR_POS_X,25)
    set_control_par(get_ui_id($controls),$CONTROL_PAR_POS_Y,50)

end on


on persistence_changed
    set_text($ring_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_RING,$group,$slot,$generic) & "%")
    set_text($fm_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_FM,$group,$slot,$generic) & "%")
    set_text($freq_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_FREQUENCY,$group,$slot,$generic) & "Hz")
    set_text($edge_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_EDGE,$group,$slot,$generic) & "%")
    set_text($amount_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_LFO_AMOUNT,$group,$slot,$generic) & "%")
    set_text($rate_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_LFO_RATE,$group,$slot,$generic) & "Hz")
end on

on ui_control($ring)
    set_engine_par($ENGINE_PAR_RINGMOD_RING,$ring,$group,$slot,$generic)
    set_text($ring_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_RING,$group,$slot,$generic) & "%")
end on

on ui_control($fm)
    set_engine_par($ENGINE_PAR_RINGMOD_FM,$fm,$group,$slot,$generic)
    set_text($fm_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_FM,$group,$slot,$generic) & "%")
end on

on ui_control($freq)
    set_engine_par($ENGINE_PAR_RINGMOD_FREQUENCY,$freq,$group,$slot,$generic)
    set_text($freq_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_FREQUENCY,$group,$slot,$generic) & "Hz")
end on

on ui_control($edge)
    set_engine_par($ENGINE_PAR_RINGMOD_EDGE,$edge,$group,$slot,$generic)
    set_text($edge_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_EDGE,$group,$slot,$generic) & "%")
end on

on ui_control($amount)
    set_engine_par($ENGINE_PAR_RINGMOD_LFO_AMOUNT,$amount,$group,$slot,$generic)
    set_text($amount_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_LFO_AMOUNT,$group,$slot,$generic) & "%")
end on

on ui_control($rate)
    set_engine_par($ENGINE_PAR_RINGMOD_LFO_RATE,$rate,$group,$slot,$generic)
    set_text($rate_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_LFO_RATE,$group,$slot,$generic) & "Hz")
end on

on ui_control($fast)
    set_engine_par($ENGINE_PAR_RINGMOD_FAST_MODE,$fast,$group,$slot,$generic)
    set_text($freq_label,get_engine_par_disp($ENGINE_PAR_RINGMOD_FREQUENCY,$group,$slot,$generic) & "Hz")
end on

on ui_control($wave)
    set_engine_par($ENGINE_PAR_RINGMOD_LFO_WAVE,$wave,$group,$slot,$generic)
end on
