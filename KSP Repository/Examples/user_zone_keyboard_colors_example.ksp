{***********************************************
User Zone Keyboard Colors Example
Author: Native Instruments
Written by: Yaron Eshkar
Modified: March 4, 2022
*************************************************}

on init
    declare @samples_path
        @samples_path :=  get_folder($GET_FOLDER_PATCH_DIR) & "Samples/" 
    declare $i
    declare ui_knob $zone_a_key_l(0,127,1)
    declare ui_knob $zone_a_key_h(0,127,1)
    declare ui_knob $zone_b_key_l(0,127,1)
    declare ui_knob $zone_b_key_h(0,127,1)
    make_persistent($zone_a_key_l)
    make_persistent($zone_a_key_h)
    make_persistent($zone_b_key_l)
    make_persistent($zone_b_key_h)
    set_num_user_zones(2)
    set_sample(%NI_USER_ZONE_IDS[0],@samples_path & "1.wav")
    set_sample(%NI_USER_ZONE_IDS[1],@samples_path & "2.wav")
    set_zone_par(%NI_USER_ZONE_IDS[0],$ZONE_PAR_GROUP,0)
    set_zone_par(%NI_USER_ZONE_IDS[1],$ZONE_PAR_GROUP,1)
end on

function set_keyboard_colors
    $i := 0
    while($i<127)
        set_key_color($i,$KEY_COLOR_WHITE)
        if( ($i>$zone_a_key_l-1) and ($i<$zone_a_key_h+1) )
            if( ($i<$zone_b_key_l) )
                set_key_color($i,$KEY_COLOR_YELLOW)
            else
                if ( ($i>$zone_b_key_l-1) and ($i<$zone_b_key_h+1) )
                    set_key_color($i,$KEY_COLOR_RED)
                else
                    set_key_color($i,$KEY_COLOR_YELLOW)
                end if
            end if
        else
            if( ($i>$zone_b_key_l-1) and ($i<$zone_b_key_h+1) )
                set_key_color($i,$KEY_COLOR_ORANGE)
            end if
        end if
        inc($i)
    end while
end function

function set_zone_a_key
    if($zone_a_key_l>$zone_a_key_h)
        $zone_a_key_l := $zone_a_key_h
    end if
    set_zone_par(%NI_USER_ZONE_IDS[0],$ZONE_PAR_LOW_KEY,$zone_a_key_l)
    set_zone_par(%NI_USER_ZONE_IDS[0],$ZONE_PAR_HIGH_KEY,$zone_a_key_h)
    call set_keyboard_colors
end function

function set_zone_b_key
    if($zone_b_key_l>$zone_b_key_h)
        $zone_b_key_l := $zone_b_key_h
    end if
    set_zone_par(%NI_USER_ZONE_IDS[1],$ZONE_PAR_LOW_KEY,$zone_b_key_l)
    set_zone_par(%NI_USER_ZONE_IDS[1],$ZONE_PAR_HIGH_KEY,$zone_b_key_h)
    call set_keyboard_colors
end function

on ui_control($zone_a_key_l)
    call set_zone_a_key
end on

on ui_control($zone_a_key_h)
    call set_zone_a_key
end on

on ui_control($zone_b_key_l)
    call set_zone_b_key
end on

on ui_control($zone_b_key_h)
    call set_zone_b_key
end on