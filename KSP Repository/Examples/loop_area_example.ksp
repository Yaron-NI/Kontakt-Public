{***********************************************
Loop Area Example
Author: Native Instruments
Written by: Yaron Eshkar
Modified: March 24, 2022
*************************************************}

on init
    message( "" )
    declare const $view_width := 250
    declare const $view_height := 60
    declare $play_pos
    declare $sample_length
    declare ~loop_percent
    make_perfview
    set_ui_color(9656966h)
    set_ui_height(3)
    set_num_user_zones(1)
        set_zone_par(%NI_USER_ZONE_IDS[0],$ZONE_PAR_HIGH_KEY,127)
        set_zone_par(%NI_USER_ZONE_IDS[0],$ZONE_PAR_HIGH_VELO,127)
    declare @sample_path
        make_persistent(@sample_path)
    declare ui_panel $view_panel
        set_control_par(get_ui_id($view_panel),$CONTROL_PAR_POS_X,100)
        set_control_par(get_ui_id($view_panel),$CONTROL_PAR_POS_Y,25)
    declare ui_mouse_area $drop_area
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_PARENT_PANEL,get_ui_id($view_panel))
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_POS_Y,25)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_HEIGHT,$view_height)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_WIDTH,$view_width)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_DND_ACCEPT_AUDIO, $NI_DND_ACCEPT_ONE)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_RECEIVE_DRAG_EVENTS,1)
    declare ui_waveform $wave_view(1,1)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_PARENT_PANEL,get_ui_id($view_panel))
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_POS_Y,25)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_HEIGHT,$view_height)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_WIDTH,$view_width)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_HIDE,$HIDE_PART_BG)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_BG_ALPHA,50)
        attach_zone($wave_view,%NI_USER_ZONE_IDS[0],0)
    declare ui_value_edit $loop_start(0,10000000,1)
        make_persistent($loop_start)
        set_control_par(get_ui_id($loop_start),$CONTROL_PAR_PARENT_PANEL,get_ui_id($view_panel))
        set_control_par(get_ui_id($loop_start),$CONTROL_PAR_WIDTH,150)
    declare ui_value_edit $loop_length(0,10000000,1)
        make_persistent($loop_length)
        set_control_par(get_ui_id($loop_length),$CONTROL_PAR_PARENT_PANEL,get_ui_id($view_panel))
        set_control_par(get_ui_id($loop_length),$CONTROL_PAR_POS_X,150)
        set_control_par(get_ui_id($loop_length),$CONTROL_PAR_WIDTH,150)
    declare ui_label $loop_label(1,1)
        set_control_par(get_ui_id($loop_label),$CONTROL_PAR_PARENT_PANEL,get_ui_id($view_panel))
        set_control_par(get_ui_id($loop_label),$CONTROL_PAR_POS_Y,25)
        set_control_par_str(get_ui_id($loop_label),$CONTROL_PAR_PICTURE,"loop")
        set_text($loop_label,"")
        set_control_par(get_ui_id($loop_label),$CONTROL_PAR_HEIGHT,$view_height)
        set_control_par(get_ui_id($loop_label),$CONTROL_PAR_WIDTH,$view_width)
end on

function set_loop
    wait_async(set_loop_par(%NI_USER_ZONE_IDS[0],0,$LOOP_PAR_START,$loop_start))
    set_control_par(get_ui_id($loop_start),$CONTROL_PAR_VALUE,get_loop_par(%NI_USER_ZONE_IDS[0],0,$LOOP_PAR_START)) { clip value }
    wait_async(set_loop_par(%NI_USER_ZONE_IDS[0],0,$LOOP_PAR_LENGTH,$loop_length))
    set_control_par(get_ui_id($loop_length),$CONTROL_PAR_VALUE,get_loop_par(%NI_USER_ZONE_IDS[0],0,$LOOP_PAR_LENGTH)) { clip value }
    ~loop_percent := int_to_real($loop_start) / int_to_real($sample_length)
    set_control_par(get_ui_id($loop_label),$CONTROL_PAR_POS_X,real_to_int(~loop_percent * int_to_real($view_width)))
    ~loop_percent := int_to_real($loop_length) / int_to_real($sample_length)
    set_control_par(get_ui_id($loop_label),$CONTROL_PAR_WIDTH,real_to_int(~loop_percent * int_to_real($view_width)))
end function

function set_sample_file
    wait_async(set_sample(%NI_USER_ZONE_IDS[0],@sample_path))
    wait_async(set_loop_par(%NI_USER_ZONE_IDS[0],0,$LOOP_PAR_MODE,1))
    $sample_length := get_zone_par(%NI_USER_ZONE_IDS[0],$ZONE_PAR_SAMPLE_END)
end function

on persistence_changed
    call set_sample_file
    call set_loop
end on

on ui_control ($drop_area)
    if ($NI_MOUSE_EVENT_TYPE = $NI_MOUSE_EVENT_TYPE_DROP)
        if (num_elements(!NI_DND_ITEMS_AUDIO) = 1)
            @sample_path := !NI_DND_ITEMS_AUDIO[0]
            call set_sample_file
        end if
    end if
end on

on ui_control($loop_start)
    call set_loop
end on

on ui_control($loop_length)
    call set_loop
end on

on note
    while($NOTE_HELD = 1)
        $play_pos := get_event_par($EVENT_ID,$EVENT_PAR_PLAY_POS)
        set_ui_wf_property($wave_view,$UI_WF_PROP_PLAY_CURSOR,0,$play_pos)
        wait (10000)
    end while
end on

on release
    set_ui_wf_property($wave_view,$UI_WF_PROP_PLAY_CURSOR,0,0)
end on  
