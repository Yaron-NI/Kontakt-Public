{***********************************************
6 Pole SV Filters Example
Author: Native Instruments
Written by: Yaron Eshkar
Modified: October 13, 2022
*************************************************}

on init

    message("")
    make_perfview

    declare ui_switch $play_note

    declare ui_switch $delay
    declare ui_switch $lfo1
    declare ui_switch $lfo2

    declare ui_menu $filters_menu
        add_menu_item($filters_menu,"SV LP6",$FILTER_TYPE_SV_LP6)
        add_menu_item($filters_menu,"SV BP6",$FILTER_TYPE_SV_BP6)
        add_menu_item($filters_menu,"SV HP6",$FILTER_TYPE_SV_HP6)
        add_menu_item($filters_menu,"SV NT6",$FILTER_TYPE_SV_NOTCH6)
        add_menu_item($filters_menu,"SV LP4",$FILTER_TYPE_SV_LP4)
        add_menu_item($filters_menu,"SV BP4",$FILTER_TYPE_SV_BP4)
        add_menu_item($filters_menu,"SV HP4",$FILTER_TYPE_SV_HP4)
        add_menu_item($filters_menu,"SV NT4",$FILTER_TYPE_SV_NOTCH4)

    declare ui_slider $cut_slider(0,1000000)
    declare ui_slider $res_slider(0,1000000)
    declare ui_label $cut_label(1,1)
    declare ui_label $res_label(1,1)
    
    make_persistent($filters_menu)
    make_persistent($delay)
    make_persistent($lfo1)
    make_persistent($lfo2)
    make_persistent($cut_slider)
    make_persistent($res_slider)

    set_engine_par($ENGINE_PAR_EFFECT_TYPE,$EFFECT_TYPE_FILTER,0,0,-1)
    set_engine_par($ENGINE_PAR_EFFECT_SUBTYPE,$filters_menu,0,0,-1)

    set_engine_par($ENGINE_PAR_EFFECT_TYPE,$EFFECT_TYPE_REPLIKA,-1,0,1)
    set_engine_par($ENGINE_PAR_EFFECT_TYPE,$EFFECT_TYPE_RING_MOD,-1,1,1)
end on

on ui_control($filters_menu)
    set_engine_par($ENGINE_PAR_EFFECT_SUBTYPE,$filters_menu,0,0,-1)
end on

on ui_control($cut_slider)
    set_engine_par($ENGINE_PAR_CUTOFF,$cut_slider,0,0,-1)
    set_text($cut_label,"Cut: " & get_engine_par_disp($ENGINE_PAR_CUTOFF,0,0,-1) & "Hz")
end on

on ui_control($res_slider)
    set_engine_par($ENGINE_PAR_RESONANCE,$res_slider,0,0,-1)
    set_text($res_label,"Res: " & get_engine_par_disp($ENGINE_PAR_RESONANCE,0,0,-1) & "%")
end on

on ui_control($delay)
    set_engine_par($ENGINE_PAR_EFFECT_BYPASS,1-$delay,-1,0,1)
end on

on ui_control($lfo1)
    set_engine_par($ENGINE_PAR_MOD_TARGET_INTENSITY,$lfo1*500000,0,1,0)
end on

on ui_control($lfo2)
    set_engine_par($ENGINE_PAR_MOD_TARGET_INTENSITY,$lfo2*500000,0,2,0)
end on

on persistence_changed
    set_engine_par($ENGINE_PAR_EFFECT_BYPASS,1-$delay,-1,0,1)
    set_text($cut_label,"Cut: " & get_engine_par_disp($ENGINE_PAR_CUTOFF,0,0,-1) & "Hz")
    set_text($res_label,"Res: " & get_engine_par_disp($ENGINE_PAR_RESONANCE,0,0,-1) & "%")
end on

on ui_control($play_note)
    if($play_note=1)
        play_note(36,127,0,-1)
    else
        note_off($ALL_EVENTS)
    end if
end on

