{***********************************************
Sample Drag and Drop Example
Author: Native Instruments
Written by: Yaron Eshkar
Modified: March 23, 2022
*************************************************}

on init
    make_perfview
    set_ui_height(8)
    set_num_user_zones(1)
    declare @sample_path
        make_persistent(@sample_path)
    declare ui_mouse_area $drop_area
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_HEIGHT,500)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_WIDTH,500)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_DND_ACCEPT_AUDIO,$NI_DND_ACCEPT_ONE)
        set_control_par(get_ui_id($drop_area),$CONTROL_PAR_RECEIVE_DRAG_EVENTS,1)
    declare ui_waveform $wave_view(1,1)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_HEIGHT,500)
        set_control_par(get_ui_id($wave_view),$CONTROL_PAR_WIDTH,500)
        attach_zone($wave_view,%NI_USER_ZONE_IDS[0],0)
end on

on persistence_changed
    set_sample(%NI_USER_ZONE_IDS[0],@sample_path)
end on

on ui_control ($drop_area)
    if ($NI_MOUSE_EVENT_TYPE = $NI_MOUSE_EVENT_TYPE_DROP)
        if (num_elements(!NI_DND_ITEMS_AUDIO) = 1)
            @sample_path := !NI_DND_ITEMS_AUDIO[0]
            wait_async(set_sample(%NI_USER_ZONE_IDS[0],@sample_path))
        end if
    end if
end on