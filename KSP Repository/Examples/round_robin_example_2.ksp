{***********************************************
Round Robin Example 2
Author: Native Instruments
Written by: Yaron Eshkar
Modified: May 17, 2021
*************************************************}

on init
    make_perfview
    set_ui_color(9000000h)
    declare ui_label $instrument_name(2,1)
        set_text($instrument_name,"My Simple RR Instrument")
    declare $rr_count := 0

    declare ui_slider $saturate_slider(500000,1000000)
        make_persistent($saturate_slider)
    declare ui_label $saturate_label(1,1)
end on

function set_control
    set_engine_par($ENGINE_PAR_SHAPE,$saturate_slider,-1,2,1)
    set_text($saturate_label,"Saturate: " & get_engine_par_disp($ENGINE_PAR_SHAPE,-1,2,1) & "%")
end function

on ui_control($saturate_slider)
    call set_control
end on

on persistence_changed
    call set_control
end on

on note
    disallow_group($ALL_GROUPS)
    allow_group($rr_count)
    if($rr_count=0)
        $rr_count := 1
    else
        $rr_count := 0
    end if
end on